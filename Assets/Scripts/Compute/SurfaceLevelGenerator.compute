#pragma kernel GenerateSurfaceLevel

#include "Imported/Noise.compute"

static const int numThreadsSurfaceLevel = 5;
struct Point { // Appears twice since importing in HLSL is a headache
	float3 position;	
	float surfaceLevel;
	float3 onEdges;
};

RWStructuredBuffer<Point> points;
AppendStructuredBuffer<int> relevant;
AppendStructuredBuffer<int> irrelevant;

int numPointsX;
int numPointsY;
int numPointsZ;

float noiseScale;
int octaves;
float3 offset;
float weightMultiplier;
float persistence;
float lacunarity;
float floorOffset;
float noiseWeight;
float4 params;
float hardFloor;
float hardFloorWeight;
float offsetNoise; // basically seed

float isoLevel;

float generateNoise(float3 pos)
{		
	float noise = 0;
	float frequency = noiseScale / 100;	
	float amplitude = 1;
	float weight = 1;	
	for (int j = 0; j < octaves; j++) {
		float n = snoise((pos + offsetNoise) * frequency);
		float v = 1 - abs(n);
		v = v * v;
		v *= weight;
		weight = max(min(v * weightMultiplier, 1), 0);
		noise += v * amplitude;
		amplitude *= persistence;
		frequency *= lacunarity;
	}
	float finalVal = -(pos.y + floorOffset) + noise * noiseWeight + (pos.y%params.x) * params.y;	
	if (pos.y < hardFloor) {
		finalVal += hardFloorWeight;
	}

	return finalVal;
}

int indexFromCoords(uint x, uint y, uint z)
{	
	return z * numPointsX * numPointsY + y * numPointsX + x;
}

float3 calculateOnEdges(uint x, uint y, uint z)
{
	float3 onEdges = float3(0, 0, 0);
	if (x + 1 == numPointsX)
	{
		onEdges.x = 1;
	}
	else if (x == 0)
	{
		onEdges.x = -1;
	}

	if (y + 1 == numPointsY)
	{
		onEdges.y = 1;
	}
	else if (y == 0)
	{
		onEdges.y = -1;
	}

	if (z + 1 == numPointsZ)
	{
		onEdges.z = 1;
	}
	else if (z == 0)
	{
		onEdges.z = -1;
	}
	return onEdges;
}

[numthreads(numThreadsSurfaceLevel, numThreadsSurfaceLevel, numThreadsSurfaceLevel)]
void GenerateSurfaceLevel(uint3 id : SV_DispatchThreadID)
{
	if (id.x < numPointsX && id.y < numPointsY && id.z < numPointsZ) {
		float3 pos = float3(id.x, id.y, id.z) + offset + float3(sign(id.x), sign(id.y), sign(id.z));		
		float generatedNoise = generateNoise(pos);		
		int index = indexFromCoords(id.x, id.y, id.z);		
		
		Point p;
		p.position = pos;
		p.surfaceLevel = -pos.y + generatedNoise;
		if (p.surfaceLevel < isoLevel) {
			relevant.Append(1);
		} else {
			irrelevant.Append(1);
		}
		p.onEdges = calculateOnEdges(id.x, id.y, id.z);
		points[index] = p;		
	}
}
